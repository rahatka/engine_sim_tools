import "engine_sim.mr"
import "../../fuel/90.mr"

units units()
constants constants()

// Engine Sim V0.1.14A
// 4-rotor
// wankel_4
// Created by oror 2023
// 1-3-2-4

label rotor_sides(3.0)
label rotors(4.0)
label rotor_initial_angle(60.0 * units.deg)

label rot360(360.0 * units.deg)
label cycle(rotor_sides * rot360)

label intake_port_center(42.0) // + degrees relative to TDC axis
label exhaust_port_center(46.0) // - degrees relative to TDC axis

label intake_port_duration(26.0) // degrees
label exhaust_port_duration(21.0) // degrees

label stroke(3.0 * units.cm)
label radius(10.5 * units.cm)
label depth(8.0) // units.cm
label depression_volume(24.0 * units.cc) // 10.0 CR?
label intake_port_start((180.0 - intake_port_center - intake_port_duration / 2) * units.deg)
label intake_port_end((180.0 - intake_port_center + intake_port_duration / 2) * units.deg)
label exhaust_port_start((180.0 + exhaust_port_center - exhaust_port_duration / 2) * units.deg)
label exhaust_port_end((180.0 + exhaust_port_center + exhaust_port_duration / 2) * units.deg)
label rotor_mass(4328 * units.g)
label rotor_I(disk_moment_of_inertia(mass: rotor_mass, radius: radius))
label intake_runner_area(intake_port_duration / 24.0 * (depth * 0.75)) // units.cm2
label exhaust_runner_area(exhaust_port_duration / 24.0 * (depth * 0.75)) // units.cm2
label intake_flow(k_carb(intake_runner_area * 66.0))
label exhaust_flow(k_carb(exhaust_runner_area * 66.0))

label volume(2.6) // L
label redline(9500.0) // RPM
label intake_cfm((volume * redline) / 56.6)
label exhaust_cfm(intake_cfm / 1.5)

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
}

private node eng {
    alias output __out: engine;

    wankel_engine engine(
        name: "Mazda 26b 4-rotor",
        starter_torque: 100 * units.Nm,
        starter_speed: 1000 * units.rpm,
        redline: redline * units.rpm,
        fuel: s90(),
        throttle_gamma: 1.8,
        noise: 0.5,
        jitter: 0.1,
        hf_gain: 0.005,
        simulation_frequency: 16000,
        rotor_calculation_quality: 1024,
        rotor_housing_resolution: 1024
    )

    wires wires()

    crankshaft c0(
        throw: 0.5 * stroke,
        flywheel_mass: 2 * units.kg,
        mass: 7 * units.kg,
        friction_torque: 20.0 * units.Nm,
        moment_of_inertia: 0.08,
        tdc: 0.0
    )

    rod_journal rj0(angle: (0.0 / rotors) * rot360)
    rod_journal rj1(angle: (2.5 / rotors) * rot360)
    rod_journal rj2(angle: (3.0 / rotors) * rot360)
    rod_journal rj3(angle: (1.5 / rotors) * rot360)

    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    engine.add_crankshaft(c0)

    wankel_rotor rotor0(
        mass: rotor_mass,
        moment_of_inertia: rotor_I,
        radius: radius,
        crown_gear_radius: 3 * 0.5 * stroke,
        angle: rotor_initial_angle + (0.0 / (rotors * rotor_sides)) * rot360,
        depth: depth * units.cm,
        depression_volume: depression_volume
    )

    wankel_rotor rotor1(
        mass: rotor_mass,
        moment_of_inertia: rotor_I,
        radius: radius,
        crown_gear_radius: 3 * 0.5 * stroke,
        angle: rotor_initial_angle + (2.5 / (rotors * rotor_sides)) * rot360,
        depth: depth * units.cm,
        depression_volume:depression_volume
    )

    wankel_rotor rotor2(
        mass: rotor_mass,
        moment_of_inertia: rotor_I,
        radius: radius,
        crown_gear_radius: 3 * 0.5 * stroke,
        angle: rotor_initial_angle + (3.0 / (rotors * rotor_sides)) * rot360,
        depth: depth * units.cm,
        depression_volume: depression_volume
    )
    
    wankel_rotor rotor3(
        mass: rotor_mass,
        moment_of_inertia: rotor_I,
        radius: radius,
        crown_gear_radius: 3 * 0.5 * stroke,
        angle: rotor_initial_angle + (1.5 / (rotors * rotor_sides)) * rot360,
        depth: depth * units.cm,
        depression_volume: depression_volume
    )

    label intake_plenum_area(intake_runner_area * rotors / 2.0) // cm2
    label collector_cross_section_area(exhaust_runner_area * rotors / 2.0) // cm2

    label exhaust_pipe_length_0(150.0) // cm
    label exhaust_volume_0(collector_cross_section_area * exhaust_pipe_length_0 / 100.0) // Litres

    label spacing_factor(1.1)
    label flange_density(1.8)
    
    intake intake(
        plenum_volume: 2.0 * units.L,
        plenum_cross_section_area: intake_plenum_area * units.cm2,
        intake_flow_rate: k_carb(intake_cfm),
        idle_flow_rate: k_carb(0.1),
        idle_throttle_plate_position: 0.983,
        runner_flow_rate: k_carb(intake_cfm / rotors),
        runner_length: 10.0 * units.cm,
        velocity_decay: 0.05
    )

    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(exhaust_cfm),
        primary_tube_length: 25.0 * units.cm,
        collector_cross_section_area: collector_cross_section_area * units.cm2,
        primary_flow_rate: k_carb(exhaust_cfm / rotors),
        velocity_decay: 1.0,
        length: exhaust_pipe_length_0 * units.cm,
        backflow_atmospheric_mixing: 0.5,
        volume: exhaust_volume_0 * units.L
    )
 
    exhaust_system exhaust0(es_params0, impulse_response: impulse_response(filename: "../../impulse_responses/smooth_20.wav", volume: 0.001))
    
    label pl0 ((4.0 * depth * spacing_factor / flange_density) * units.cm)
    label pl1 ((3.0 * depth * spacing_factor / flange_density) * units.cm)
    label pl2 ((2.0 * depth * spacing_factor / flange_density) * units.cm)
    label pl3 ((1.0 * depth * spacing_factor / flange_density) * units.cm)

    engine.add_rotor(
        intake,
        exhaust0,
        rotor0,
        rj0,
        wires.wire1,
        primary_length: pl0,
        intake_port: default_wankel_peripheral_port(intake_port_start, intake_port_end, intake_flow),
        exhaust_port: default_wankel_peripheral_port(exhaust_port_start, exhaust_port_end, exhaust_flow),
        intake_runner_cross_section_area: intake_runner_area * units.cm2,
        exhaust_cross_section_area: exhaust_runner_area * units.cm2,
        spark_plug_angle: 0
    )
    engine.add_rotor(
        intake,
        exhaust0,
        rotor1,
        rj1,
        wires.wire2,
        primary_length: pl1,
        intake_port: default_wankel_peripheral_port(intake_port_start, intake_port_end, intake_flow),
        exhaust_port: default_wankel_peripheral_port(exhaust_port_start, exhaust_port_end, exhaust_flow),
        intake_runner_cross_section_area: intake_runner_area * units.cm2,
        exhaust_cross_section_area: exhaust_runner_area * units.cm2,
        spark_plug_angle: 0
    )
    engine.add_rotor(
        intake,
        exhaust0,
        rotor2,
        rj2,
        wires.wire3,
        primary_length: pl2,
        intake_port: default_wankel_peripheral_port(intake_port_start, intake_port_end, intake_flow),
        exhaust_port: default_wankel_peripheral_port(exhaust_port_start, exhaust_port_end, exhaust_flow),
        intake_runner_cross_section_area: intake_runner_area * units.cm2,
        exhaust_cross_section_area: exhaust_runner_area * units.cm2,
        spark_plug_angle: 0
    )
    engine.add_rotor(
        intake,
        exhaust0,
        rotor3,
        rj3,
        wires.wire4,
        primary_length: pl3,
        intake_port: default_wankel_peripheral_port(intake_port_start, intake_port_end, intake_flow),
        exhaust_port: default_wankel_peripheral_port(exhaust_port_start, exhaust_port_end, exhaust_flow),
        intake_runner_cross_section_area: intake_runner_area * units.cm2,
        exhaust_cross_section_area: exhaust_runner_area * units.cm2,
        spark_plug_angle: 0
    )

    // ignition_timing.py v0.1 r = 1.15
    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 8.0 * units.deg)
        .add_sample(1000 * units.rpm, 9.2 * units.deg)
        .add_sample(2000 * units.rpm, 10.6 * units.deg)
        .add_sample(3000 * units.rpm, 12.2 * units.deg)
        .add_sample(4000 * units.rpm, 14.0 * units.deg)
        .add_sample(5000 * units.rpm, 16.1 * units.deg)
        .add_sample(6000 * units.rpm, 18.5 * units.deg)
        .add_sample(7000 * units.rpm, 21.3 * units.deg)
        .add_sample(8000 * units.rpm, 24.5 * units.deg)
        .add_sample(9000 * units.rpm, 28.1 * units.deg)
        .add_sample(10000 * units.rpm, 32.4 * units.deg)

    engine.add_ignition_module(
        ignition_module(timing_curve: timing_curve, rev_limit: 10000 * units.rpm, limiter_duration: 0.03)
            .connect_wire(wires.wire1, (0.0 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire4, (1.5 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire2, (2.5 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire3, (3.0 / (rotors * rotor_sides)) * cycle)
            
            .connect_wire(wires.wire1, (4.0 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire4, (5.5 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire2, (6.5 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire3, (7.0 / (rotors * rotor_sides)) * cycle)
            
            .connect_wire(wires.wire1, (8.0 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire4, (9.5 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire2, (10.5 / (rotors * rotor_sides)) * cycle)
            .connect_wire(wires.wire3, (11.0 / (rotors * rotor_sides)) * cycle)
    )
}

// 93-95 RX7 Turbo
public node veh {
    alias output __out: vehicle;
    vehicle vehicle(
        mass: 1284  * units.kg,
        drag_coefficient: 0.33,
        cross_sectional_area: (1750  * units.mm) * (1230 * units.mm),
        diff_ratio: 4.1,
        tire_radius: (632.0 / 2) * units.mm, // 225/50ZR16
        rolling_resistance: 20,
        damping: 15.0,
        max_flex: 5 * units.deg,
        limit_flex: true,
        simulate_flex: true
        )
}

// 93-95 RX7 Turbo
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 750 * units.Nm,
            max_clutch_flex: 10 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 75 * units.Nm / units.deg,
            clutch_damping: 2.0,
            simulate_flex: true
        )
        .add_gear(3.483)
        .add_gear(2.015)
        .add_gear(1.391)
        .add_gear(1.0)
        .add_gear(0.719);
}

run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)
