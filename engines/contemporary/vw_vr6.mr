/*
{
   "engine":{
      "es_version":"V0.1.14A",
      "name":"Volkswagen 3.2 VR6 (R32) 2003",
      "catalog_id":247,
      "yt_handle":"5D2hi9oxsqs",
      "enable_update":true,
      "description":[
         "3.2L VR6 from VAG cars (golf, audi tt, etc).",
         "Finally employing variable primary length in v1.11 to simulate a distinctive VW VR6 sound.",
         "1-5-3-6-2-4"
      ]
   }
}
*/

// VW VR6 241 HP @ 6250
// vw_vr6
// Created by oror 2023
// 1-5-3-6-2-4

import "engine_sim.mr"
import "../../fuel/30.mr"

units units()
constants constants()

label bore(84.0)
label stroke(95.9)
label compression_ratio(11.3)
label con_rod(164.0)
label compression_height(35.0 * units.mm) // ?
label intake_valve_diameter(31.0)
label exhaust_valve_diameter(27.0)
label intake_valves(2.0)
label exhaust_valves(2.0)

label volume(3.189) // L; port_flow.py v0.6
label redline(6500.0)

label cyl(6.0)
label row(1.0)
label cycle(720.0 * units.deg)
label vee(15.0 * units.deg)
label rot(cycle / cyl)
label rot90(90.0 * units.deg)
label rot180(180.0 * units.deg)
label rot360(360.0 * units.deg)

// VW VR6 268Â° Sportnockenwellen
label IVL(11.4)
label EVL(11.4)
label IVO(19.0 * units.deg) //BTDC
label IVC(69.0 * units.deg) //ABDC
label EVO(69.0 * units.deg) //BBDC
label EVC(19.0 * units.deg) //ATDC

label intake_duration(IVO + IVC + rot180)
label exhaust_duration(EVO + EVC + rot180)

label ILC((rot180 - IVO + IVC) / 2.0 + rot360)
label ELC((rot180 - EVO + EVC) / 2.0 + rot180)

label exhaust_delay_coeff(1.5)

public node eng_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 7000 * units.rpm;
    input limiter_duration: 0.03;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, rot * 0)
            .connect_wire(wires.wire5, rot * 1)
            .connect_wire(wires.wire3, rot * 2)
            .connect_wire(wires.wire6, rot * 3)
            .connect_wire(wires.wire2, rot * 4)
            .connect_wire(wires.wire4, rot * 5);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

public node intake_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: intake_duration,
            gamma: 4.0,
            lift: IVL * units.mm,
            steps: 100
        );
}

public node exhaust_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: exhaust_duration,
            gamma: 4.0,
            lift: EVL * units.mm,
            steps: 100
        );
}

public node camshaft_builder {
    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;

    camshaft _intake_cam_0(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_1(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)

    _intake_cam_0
        .add_lobe(ILC + 0 * rot) // 1
        .add_lobe(ILC + 2 * rot) // 3
        .add_lobe(ILC + 1 * rot) // 5
        
    _exhaust_cam_0
        .add_lobe(ELC + 0 * rot)
        .add_lobe(ELC + 2 * rot)
        .add_lobe(ELC + 1 * rot)
        
    _intake_cam_1
        .add_lobe(ILC + 4 * rot) // 2
        .add_lobe(ILC + 5 * rot) // 4
        .add_lobe(ILC + 3 * rot) // 6

    _exhaust_cam_1
        .add_lobe(ELC + 4 * rot)
        .add_lobe(ELC + 5 * rot)
        .add_lobe(ELC + 3 * rot)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node head { // generated by port_flow.py v0.6
    // intake H: 31.0 L: 11.4 N: 2.0
    // exhaust H: 27.0 L: 11.4 N: 2.0
    // intake port area: 12.8 cm2; effective lift: 6.7 mm
    // exhaust port area: 9.7 cm2; effective lift: 5.9 mm
    // cylinder volume: 531.5 cc; engine volume: 3.189 L; 194.6 CI
    // 16 harmonic intake runner length: 14.6 cm; diameter: 4.2 cm
    // primary length: 49.9 cm, area: 9.2 cm2, diameter: 3.4 cm
    // collector diameter: 5.9 cm, area: 27.5 cm2
    // target power: 5850 RPM, torque: 3861 RPM

    input intake_camshaft;
    input exhaust_camshaft;
    input flip_display: false;
    
    alias output __out: head;
    function intake_flow(1.140 * units.mm)
    intake_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.140, 45.0)
        .add_flow_sample(2.280, 77.5)
        .add_flow_sample(3.420, 105.3)
        .add_flow_sample(4.560, 130.0)
        .add_flow_sample(5.700, 152.5)
        .add_flow_sample(6.840, 173.4)
        .add_flow_sample(7.980, 174.1)
        .add_flow_sample(9.120, 174.8)
        .add_flow_sample(10.260, 175.5)
        .add_flow_sample(11.400, 176.3)
        
    function exhaust_flow(1.140 * units.mm)
    exhaust_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.140, 38.1)
        .add_flow_sample(2.280, 65.3)
        .add_flow_sample(3.420, 88.5)
        .add_flow_sample(4.560, 108.9)
        .add_flow_sample(5.700, 127.4)
        .add_flow_sample(6.840, 131.7)
        .add_flow_sample(7.980, 132.2)
        .add_flow_sample(9.120, 132.8)
        .add_flow_sample(10.260, 133.4)
        .add_flow_sample(11.400, 134.1)
        
    generic_cylinder_head head(
        chamber_volume: 47.031 * units.cc,
        intake_runner_volume: 202.1 * units.cc,
        intake_runner_cross_section_area: 13.9 * units.cm2,
        exhaust_runner_volume: 67.4 * units.cc,
        exhaust_runner_cross_section_area: 9.2 * units.cm2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: standard_valvetrain(
            intake_camshaft: intake_camshaft,
            exhaust_camshaft: exhaust_camshaft
        ),
        flip_display: flip_display
    )
}

public node eng {
    alias output __out: engine;

    engine engine(
        name: "VW 3.2 VR6",
        starter_torque: 100 * units.Nm,
        starter_speed: 650 * units.rpm,
        redline: redline * units.rpm,
        fuel: fuel(
            max_turbulence_effect: 3.0,
            burning_efficiency_randomness: 0.1,
            max_burning_efficiency: 0.99
        ),
        throttle_gamma: 1.5,
        jitter: 0.6,
        noise: 0.2,
        hf_gain: 0.002,
        simulation_frequency: 12000
    )

    wires wires()
    
    connecting_rod_parameters cr_params(
        mass: 450.0 * units.g,
        moment_of_inertia: 0.001,
        center_of_mass: 0.0,
        length: con_rod * units.mm
    )

    crankshaft c0(
        throw: (stroke / 2.0) * units.mm,
        flywheel_mass: 6 * units.kg,
        mass: 15 * units.kg,
        friction_torque: 5.0 * units.Nm,
        moment_of_inertia: 0.2,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0
    )

    rod_journal rj0(angle: rot * 0 + rot90 + vee / 2)
    rod_journal rj1(angle: rot * 4 + rot90 - vee / 2)
    rod_journal rj2(angle: rot * 2 + rot90 + vee / 2)
    rod_journal rj3(angle: rot * 5 + rot90 - vee / 2)
    rod_journal rj4(angle: rot * 1 + rot90 + vee / 2)
    rod_journal rj5(angle: rot * 3 + rot90 - vee / 2)
    
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)

    piston_parameters piston_params(
        mass: 350 * units.g,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    cylinder_bank_parameters bank_params(
        bore: bore * units.mm,
        deck_height: (con_rod + stroke / 2.0) * units.mm + compression_height
    )

    label intake_valve_circ(intake_valve_diameter * constants.pi)
    label intake_valve_area(intake_valve_circ * IVL / 100.0) // cm2

    label intake_plenum_area(intake_valve_area * cyl * intake_valves) // cm2
    label collector_cross_section_area(27.5) // port_flow.py v0.6

    label exhaust_pipe_length_0(300.0) // cm
    label exhaust_volume_0(collector_cross_section_area * exhaust_pipe_length_0 / 100.0) // Litres
    
    label spacing_factor(0.75)
    label flange_density(1.2 * exhaust_delay_coeff)

    intake intake(
        plenum_volume: 2.8 * units.L, // port_flow.py v0.6
        plenum_cross_section_area: 41.6 * units.cm2, // port_flow.py v0.6
        intake_flow_rate: k_carb(345.4), // port_flow.py v0.6
        idle_flow_rate: k_carb(0.02),
        idle_throttle_plate_position: 0.996,
        runner_flow_rate: k_carb(115.1), // PF 1.0; port_flow.py v0.6
        runner_length: 14.6 * units.cm, // port_flow.py v0.6
        velocity_decay: 0.05
    )
    
    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(575.7), // port_flow.py v0.6
        collector_cross_section_area: 27.5 * units.cm2, // CF 1.0; port_flow.py v0.6
        length: exhaust_pipe_length_0 * units.cm,
        primary_tube_length: 49.9 * units.cm, // port_flow.py v0.6
        primary_flow_rate: k_carb(191.9), // PF 1.0; port_flow.py v0.6
        velocity_decay: 1.0,
        volume: (exhaust_volume_0 + 3.0) * units.L
    )

    exhaust_system exhaust0(es_params0, impulse_response: impulse_response(filename: "../../impulse_responses/smooth_39.wav", volume: 0.001))
 
    cylinder_bank b0(bank_params, angle: vee / 2)
    cylinder_bank b1(bank_params, angle: -vee / 2)
    
    label pl0 ((2.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl1 ((2.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl2 ((3.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl3 ((1.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl4 ((1.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl5 ((2.0 * bore * spacing_factor / flange_density + bore) * units.mm)

    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl0,
            ignition_wire: wires.wire1
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl1,
            ignition_wire: wires.wire2
        )
    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl2,
            ignition_wire: wires.wire3
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl3,
            ignition_wire: wires.wire4
        )
    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl4,
            ignition_wire: wires.wire5
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl5,
            ignition_wire: wires.wire6
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    camshaft_builder camshaft()

    b0.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )

    // ignition_timing.py v0.1 r=1.19
    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 10.0 * units.deg)
        .add_sample(1000 * units.rpm, 11.9 * units.deg)
        .add_sample(2000 * units.rpm, 14.2 * units.deg)
        .add_sample(3000 * units.rpm, 16.9 * units.deg)
        .add_sample(4000 * units.rpm, 20.1 * units.deg)
        .add_sample(5000 * units.rpm, 23.9 * units.deg)
        .add_sample(6000 * units.rpm, 28.4 * units.deg)
        .add_sample(7000 * units.rpm, 33.8 * units.deg)

    engine.add_ignition_module(
        eng_distributor(
            wires: wires,
            timing_curve: timing_curve
        ))
}

// Golf R32
public node veh {
    alias output __out: vehicle;
    vehicle vehicle(
        mass: 1541 * units.kg,
        drag_coefficient: 0.32,
        cross_sectional_area: (1759 * units.mm) * (1465 * units.mm),
        diff_ratio: 4.24,
        tire_radius: (693.0 / 2) * units.mm,
        rolling_resistance: 20
        )
}

 // Golf R32
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.Nm,
            max_clutch_flex: 30 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 10 * units.Nm / units.deg,
            clutch_damping: 5.0,
            simulate_flex: true
        )
        .add_gear(3.36)
        .add_gear(2.09)
        .add_gear(1.47)
        .add_gear(1.10)
        .add_gear(0.86)
        .add_gear(0.72);
}

run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)