// Engine Sim V0.1.14A
// Porsche 718 GT4
// porsche_982_gt4
// Created by oror 2023

import "engine_sim.mr"
import "_fuel_30.mr"

units units()
constants constants()

label bore(102.0)
label stroke(81.5)
label compression_ratio(12.5)
label con_rod(129.0)
label compression_height(31.7 * units.mm)
label intake_valve_diameter(39.5) // 4028977934698
label exhaust_valve_diameter(36.1) // 4028977934681
label intake_valves(2.0)
label exhaust_valves(2.0)

label volume(3.996) // L
label redline(8500.0) // RPM

label cyl(6.0)
label row(2.0)
label cycle(720.0 * units.deg)
label vee(180.0 * units.deg)
label rot90(90.0 * units.deg)
label rot180(180.0 * units.deg)
label rot360(360.0 * units.deg)
label rot(cycle / cyl)

// 288/278 108LSA
label IVL(13.5) // 
label EVL(12.0) // 
label IVO(36.0 * units.deg) //BTDC
label IVC(72.0 * units.deg) //ABDC
label EVO(67.0 * units.deg) //BBDC
label EVC(31.0 * units.deg) //ATDC

label intake_duration(IVO + IVC + rot180)
label exhaust_duration(EVO + EVC + rot180)

label ILC((rot180 - IVO + IVC) / 2.0 + rot360)
label ELC((rot180 - EVO + EVC) / 2.0 + rot180)

label con_rod_mass(535.0) // g
label crank_mass(14.69) // kg
label flywheel_mass(3.0) // kg

label exhaust_delay_coeff(1.5)

public node eng_distributor {
    input wires;
    input timing_curve;
    input rev_limit: 9000 * units.rpm;
    input limiter_duration: 1.0 / (9000 * (cyl / 2.0) / 60.0);
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, rot * 0)
            .connect_wire(wires.wire6, rot * 1)
            .connect_wire(wires.wire2, rot * 2)
            .connect_wire(wires.wire4, rot * 3)
            .connect_wire(wires.wire3, rot * 4)
            .connect_wire(wires.wire5, rot * 5);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
}

public node intake_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: intake_duration,
            gamma: 5.0,
            lift: IVL * units.mm,
            steps: 100
        );
}

public node exhaust_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: exhaust_duration,
            gamma: 5.0,
            lift: EVL * units.mm,
            steps: 100
        );
}

public node camshaft_builder {
    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;



    camshaft _intake_cam_0(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    
    camshaft _intake_cam_1(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)

    _intake_cam_0
        .add_lobe(ILC + 0 * rot)
        .add_lobe(ILC + 2 * rot)
        .add_lobe(ILC + 4 * rot)

    _exhaust_cam_0
        .add_lobe(ELC + 0 * rot)
        .add_lobe(ELC + 2 * rot)
        .add_lobe(ELC + 4 * rot)
        
    _intake_cam_1
        .add_lobe(ILC + 3 * rot)
        .add_lobe(ILC + 5 * rot)
        .add_lobe(ILC + 1 * rot)

    _exhaust_cam_1
        .add_lobe(ELC + 3 * rot)
        .add_lobe(ELC + 5 * rot)
        .add_lobe(ELC + 1 * rot)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node head { // generated by port_flow.py v0.5
    // intake H: 39.5 L: 13.5 N: 2.0
    // exhaust H: 36.1 L: 12.0 N: 2.0
    // intake port area: 2083.210 mm2; effective lift: 8.565 mm
    // exhaust port area: 1740.016 mm2; effective lift: 7.828 mm
    // cylinder volume: 665.960 cc; engine volume: 3.996 L; 243.837 CI
    // 16 harmonic intake runner length: 10.689 cm; diameter: 5.381 cm
    // primary length: 37.536 cm, area: 15.009 cm2, diameter: 4.372 cm
    // collector diameter: 5.354 cm, area: 22.514 cm2
    // target power: 7650 RPM, torque: 5049 RPM

    input intake_camshaft;
    input exhaust_camshaft;
    input flip_display: false;
    
    alias output __out: head;
    function intake_flow(1.350 * units.mm)
    intake_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.350, 69.840)
        .add_flow_sample(2.700, 120.571)
        .add_flow_sample(4.050, 164.088)
        .add_flow_sample(5.400, 202.922)
        .add_flow_sample(6.750, 238.272)
        .add_flow_sample(8.100, 270.847)
        .add_flow_sample(9.450, 287.110)
        .add_flow_sample(10.800, 288.389)
        .add_flow_sample(12.150, 289.787)
        .add_flow_sample(13.500, 291.305)
        
    function exhaust_flow(1.200 * units.mm)
    exhaust_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.200, 60.307)
        .add_flow_sample(2.400, 104.204)
        .add_flow_sample(3.600, 141.905)
        .add_flow_sample(4.800, 175.584)
        .add_flow_sample(6.000, 206.272)
        .add_flow_sample(7.200, 234.576)
        .add_flow_sample(8.400, 252.902)
        .add_flow_sample(9.600, 253.887)
        .add_flow_sample(10.800, 254.962)
        .add_flow_sample(12.000, 256.129)
        
    cylinder_head head(
        chamber_volume: 53.277 * units.cc,
        intake_runner_volume: 243.080 * units.cc,
        intake_runner_cross_section_area: 22.741 * units.cm2,
        exhaust_runner_volume: 81.027 * units.cc,
        exhaust_runner_cross_section_area: 15.009 * units.cm2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node eng {
    alias output __out: engine;

    engine engine(
        name: "Porsche Cayman GT4",
        starter_torque: 150 *units.Nm,
        starter_speed: 500 * units.rpm,
        redline: redline * units.rpm,
        fuel: fuel(
            max_turbulence_effect: 3.0,
            burning_efficiency_randomness: 0.1,
            max_burning_efficiency: 0.99
        ),
        throttle_gamma: 1.75,
        jitter: 0.5,
        noise: 0.7,
        hf_gain: 0.002,
        simulation_frequency: 10000
    )

    wires wires()

    connecting_rod_parameters cr_params(
        mass: con_rod_mass * units.g,
        moment_of_inertia: rod_moment_of_inertia(
            mass: con_rod_mass * units.g,
            length: con_rod * units.mm
        ),
        center_of_mass: 0.0,
        length: con_rod * units.mm
    )

    label crank_moment(disk_moment_of_inertia(mass: crank_mass * units.kg, radius: (stroke / 2.0) * units.mm))
    label flywheel_moment(disk_moment_of_inertia(mass: flywheel_mass * units.kg, radius: (stroke * 3.5) * units.mm))
    label other_moment(disk_moment_of_inertia(mass: 1 * units.kg, radius: 1.0 * units.cm))

    crankshaft c0(
        throw: (stroke / 2.0) * units.mm,
        flywheel_mass: flywheel_mass * units.kg,
        mass: crank_mass * units.kg,
        friction_torque: 2.0 * units.Nm,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0
    )

    rod_journal rj0(angle: rot * 0 + vee)
    rod_journal rj2(angle: rot * 2 + vee)
    rod_journal rj4(angle: rot * 4 + vee)
    rod_journal rj1(angle: rot * 3)
    rod_journal rj3(angle: rot * 5)
    rod_journal rj5(angle: rot * 1)
    
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)
        .add_rod_journal(rj4)
        .add_rod_journal(rj5)

    piston_parameters piston_params(
        mass: 440.0 * units.g,
        blowby: k_28inH2O(0.05),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    cylinder_bank_parameters bank_params(
        bore: bore * units.mm,
        deck_height: (con_rod + (stroke / 2.0)) * units.mm + compression_height
    )

    label collector_cross_section_area(22.514) // cm2

    label exhaust_pipe_length_0(153.0) // cm
    label exhaust_volume_0(collector_cross_section_area * exhaust_pipe_length_0 / 100.0) // Litres
    
    label exhaust_pipe_length_1(150.0) // cm
    label exhaust_volume_1(collector_cross_section_area * exhaust_pipe_length_1 / 100.0) // Litres
    
    label spacing_factor(1.05)
    label flange_density(1.0 * exhaust_delay_coeff)
    

    intake intake(
        plenum_volume: 4.586 * units.L, // port_flow.py v0.5
        plenum_cross_section_area: 68.223 * units.cm2, // port_flow.py v0.5
        runner_length: 10.689 * units.cm, // port_flow.py v0.5
        intake_flow_rate: k_carb(intake_cfm),
        idle_flow_rate: k_carb(0.04),
        idle_throttle_plate_position: 0.9980,
        runner_flow_rate: k_carb(intake_cfm / (cyl / 2.0) * 1.33),
        velocity_decay: 0.05
    )
    
    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(exhaust_cfm),
        collector_cross_section_area: collector_cross_section_area * units.cm2,
        length: exhaust_pipe_length_0 * units.cm,
        primary_tube_length: 37.536 * units.cm, // port_flow.py v0.5
        primary_flow_rate: k_carb(exhaust_cfm / (cyl / 2.0)),
        velocity_decay: 1.0,
        volume: (exhaust_volume_0 + 3.0) * units.L
    )
    
    exhaust_system_parameters es_params1(
        outlet_flow_rate: k_carb(exhaust_cfm),
        collector_cross_section_area: collector_cross_section_area * units.cm2,
        length: exhaust_pipe_length_1 * units.cm,
        primary_tube_length: 37.536 * units.cm, // port_flow.py v0.5
        primary_flow_rate: k_carb(exhaust_cfm / (cyl / 2.0)),
        velocity_decay: 1.0,
        volume: (exhaust_volume_1 + 3.0) * units.L
    )

    exhaust_system exhaust0(es_params0, impulse_response: ir_lib.default_0, audio_volume: 1.0)
    exhaust_system exhaust1(es_params1, impulse_response: ir_lib.default_0, audio_volume: 0.9)

    // exhaust_system exhaust0(es_params0, audio_volume: 0.020, impulse_response: impulse_response(filename: "F:/f154.wav", volume: 0.001))
    // exhaust_system exhaust1(es_params1, audio_volume: 0.022, impulse_response: impulse_response(filename: "F:/f154.wav", volume: 0.001))
 
    cylinder_bank b0(bank_params, angle: vee / 2)
    cylinder_bank b1(bank_params, angle: -vee / 2)
    
    label pl0 ((4.2 * bore * spacing_factor / flange_density) * units.mm)
    label pl1 ((3.8 * bore * spacing_factor / flange_density) * units.mm)
    label pl2 ((3.2 * bore * spacing_factor / flange_density) * units.mm)

    b0
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl0,
            ignition_wire: wires.wire1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl1,
            ignition_wire: wires.wire2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl2,
            ignition_wire: wires.wire3
        )
        
    b1
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl0,
            ignition_wire: wires.wire4
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl1,
            ignition_wire: wires.wire5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl2,
            ignition_wire: wires.wire6
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    camshaft_builder camshaft()

    b0.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 15.0 * units.deg)
        .add_sample(1000 * units.rpm, 17.2 * units.deg)
        .add_sample(2000 * units.rpm, 19.8 * units.deg)
        .add_sample(3000 * units.rpm, 22.8 * units.deg)
        .add_sample(4000 * units.rpm, 26.2 * units.deg)
        .add_sample(5000 * units.rpm, 30.8 * units.deg)
        .add_sample(6000 * units.rpm, 33.5 * units.deg)
        .add_sample(7000 * units.rpm, 36.6 * units.deg)
        .add_sample(8000 * units.rpm, 39.9 * units.deg)

    engine.add_ignition_module(
        eng_distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// Cayman GT4
public node veh {
    alias output __out: vehicle;
    vehicle vehicle(
        mass: 1420 * units.kg,
        drag_coefficient: 0.32,
        cross_sectional_area: (1994 * units.mm) * (1269 * units.mm),
        diff_ratio: 3.89,
        tire_radius: (685.8 / 2) * units.mm, // 295/30 ZR20
        rolling_resistance: 20
        )
}

// Cayman GT4
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 600 * units.Nm,
            max_clutch_flex: 10 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 20 * units.Nm / units.deg,
            clutch_damping: 2.0,
            simulate_flex: true
        )
        .add_gear(3.31)
        .add_gear(1.95)
        .add_gear(1.41)
        .add_gear(1.13)
        .add_gear(0.95)
        .add_gear(0.81);
}

run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)
