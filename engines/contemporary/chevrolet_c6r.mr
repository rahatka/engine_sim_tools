// Engine Sim V0.1.14A
// Chevrolet LT1 6.2L V8 455 @ 6000
// chevrolet_lt1
// Created by oror 2023
// 1-3-7-2-6-5-4-8

import "engine_sim.mr"
import "_fuel_30.mr"

units units()
constants constants()

label bore(103.25)
label stroke(92.0)
label compression_ratio(11.5)
label con_rod(154.8892)
label compression_height(31.0134 * units.mm)
label intake_valve_diameter(54.0)
label exhaust_valve_diameter(40.4)
label intake_valves(1.0)
label exhaust_valves(1.0)

label volume(6.162) // L
label redline(6500.0) // RPM

label cyl(8.0)
label row(2.0)
label cycle(720.0 * units.deg)
label vee(90.0 * units.deg)
label rot(cycle / cyl)
label rot90(90.0 * units.deg)
label rot180(180.0 * units.deg)
label rot360(360.0 * units.deg)

// COMP Cams Xtreme Energy Camshafts 07-305-8
label IVL(13.9954)
label EVL(13.3096)
label IVO(24.0 * units.deg) //BTDC
label IVC(72.0 * units.deg) //ABDC
label EVO(79.0 * units.deg) //BBDC
label EVC(31.0 * units.deg) //ATDC

label intake_duration(IVO + IVC + rot180)
label exhaust_duration(EVO + EVC + rot180)

label ILC((rot180 - IVO + IVC) / 2.0 + rot360)
label ELC((rot180 - EVO + EVC) / 2.0 + rot180)

label con_rod_mass(630.0) // g
label crank_mass(21.3188) // kg
label flywheel_mass(6.0) // kg

public node distributor {
    input wires;
    input timing_curve;
    input rev_limit: 6750 * units.rpm;
    input limiter_duration: 0.09;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, rot * 0)
            .connect_wire(wires.wire3, rot * 1)
            .connect_wire(wires.wire7, rot * 2)
            .connect_wire(wires.wire2, rot * 3)
            .connect_wire(wires.wire6, rot * 4)
            .connect_wire(wires.wire5, rot * 5)
            .connect_wire(wires.wire4, rot * 6)
            .connect_wire(wires.wire8, rot * 7);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node intake_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: intake_duration,
            gamma: 4.0,
            lift: IVL * units.mm,
            steps: 100
        );
}

public node exhaust_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: exhaust_duration,
            gamma: 4.0,
            lift: EVL * units.mm,
            steps: 100
        );
}

public node camshaft_builder {
    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;

    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;



    camshaft _intake_cam_0(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    
    camshaft _intake_cam_1(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)

    _intake_cam_0
        .add_lobe(ILC + 0 * rot)
        .add_lobe(ILC + 3 * rot)
        .add_lobe(ILC + 1 * rot)
        .add_lobe(ILC + 6 * rot)
    _exhaust_cam_0
        .add_lobe(ELC + 0 * rot)
        .add_lobe(ELC + 3 * rot)
        .add_lobe(ELC + 1 * rot)
        .add_lobe(ELC + 6 * rot)
        
    _intake_cam_1
        .add_lobe(ILC + 5 * rot)
        .add_lobe(ILC + 4 * rot)
        .add_lobe(ILC + 2 * rot)
        .add_lobe(ILC + 7 * rot)
    _exhaust_cam_1
        .add_lobe(ELC + 5 * rot)
        .add_lobe(ELC + 4 * rot)
        .add_lobe(ELC + 2 * rot)
        .add_lobe(ELC + 7 * rot)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node head {
    input intake_camshaft;
    input exhaust_camshaft;
    input flip_display: false;
    
    alias output __out: head;

    // port_flow.py v0.4
    // intake H: 54.0 L: 13.9954 N: 1.0
    // exhaust H: 40.4 L: 13.3096 N: 1.0
    // intake port area: 1946.688 mm2; effective lift: 11.709 mm
    // exhaust port area: 1089.611 mm2; effective lift: 8.760 mm
    // cylinder volume: 770.296 cc; engine volume: 6.162 L; 376.052 CI
    // 16 harmonic intake runner length: 14.326 cm; diameter: 5.061 cm
    // primary length: 96.086 cm, area: 13.276 cm2, diameter: 4.111 cm
    // collector diameter: 5.814 cm, area: 26.551 cm2
    // target power: 5850 RPM, torque: 3861 RPM
    function intake_flow(1.400 * units.mm)
    intake_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.400, 52.193)
        .add_flow_sample(2.799, 90.844)
        .add_flow_sample(4.199, 124.384)
        .add_flow_sample(5.598, 154.605)
        .add_flow_sample(6.998, 182.358)
        .add_flow_sample(8.397, 208.144)
        .add_flow_sample(9.797, 232.294)
        .add_flow_sample(11.196, 255.044)
        .add_flow_sample(12.596, 269.944)
        .add_flow_sample(13.995, 271.148)

    function exhaust_flow(1.331 * units.mm)
    exhaust_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.331, 37.494)
        .add_flow_sample(2.662, 64.803)
        .add_flow_sample(3.993, 88.267)
        .add_flow_sample(5.324, 109.235)
        .add_flow_sample(6.655, 128.346)
        .add_flow_sample(7.986, 145.978)
        .add_flow_sample(9.317, 158.651)
        .add_flow_sample(10.648, 159.331)
        .add_flow_sample(11.979, 160.073)
        .add_flow_sample(13.310, 160.878)

    cylinder_head head(
        chamber_volume: (circle_area(bore / 2.0) * stroke / compression_ratio) / 1000.0 * units.cc,
        intake_runner_volume: 288.155 * units.cc,
        intake_runner_cross_section_area: 20.115 * units.cm2,
        exhaust_runner_volume: 96.052 * units.cc,
        exhaust_runner_cross_section_area: 13.276 * units.cm2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        intake_camshaft: intake_camshaft,
        exhaust_camshaft: exhaust_camshaft,
        flip_display: flip_display
    )
}

public node eng {
    alias output __out: engine;

    engine engine(
        name: "Chevrolet LT1",
        starter_torque: 200 * units.Nm,
        starter_speed: 400 * units.rpm,
        redline: redline * units.rpm,
        fuel: fuel(
            max_turbulence_effect: 2.0,
            burning_efficiency_randomness: 0.2,
            max_burning_efficiency: 0.99
        ),
        throttle_gamma: 1.5,
        jitter: 0.2,
        noise: 0.2,
        hf_gain: 0.02,
        simulation_frequency: 12000 
    )

    wires wires()

    connecting_rod_parameters cr_params(
        mass: con_rod_mass * units.g,
        moment_of_inertia: rod_moment_of_inertia(
            mass: con_rod_mass * units.g,
            length: con_rod * units.mm
        ),
        center_of_mass: 0.0,
        length: con_rod * units.mm
    )

    label crank_moment(disk_moment_of_inertia(mass: crank_mass * units.kg, radius: (stroke / 2.0) * units.mm))
    label flywheel_moment(disk_moment_of_inertia(mass: flywheel_mass * units.kg, radius: (stroke * 3.5) * units.mm))
    label other_moment(disk_moment_of_inertia(mass: 1 * units.kg, radius: 1.0 * units.cm))

    crankshaft c0(
        throw: (stroke / 2.0) * units.mm,
        flywheel_mass: flywheel_mass * units.kg,
        mass: crank_mass * units.kg,
        friction_torque: 7.0 * units.Nm,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0
    )

    rod_journal rj0(angle: rot * 0 + rot90 + vee / 2)
    rod_journal rj1(angle: rot * 3 + rot90 + vee / 2)
    rod_journal rj2(angle: rot * 1 + rot90 + vee / 2)
    rod_journal rj3(angle: rot * 6 + rot90 + vee / 2)
    
    c0
        .add_rod_journal(rj0)
        .add_rod_journal(rj1)
        .add_rod_journal(rj2)
        .add_rod_journal(rj3)

    piston_parameters piston_params(
        mass: 372.0 * units.g,
        blowby: k_28inH2O(0.05),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    cylinder_bank_parameters bank_params(
        bore: bore * units.mm,
        deck_height: (con_rod + stroke / 2.0) * units.mm + compression_height
    )

    label collector_cross_section_area(25.734) // cm2

    label exhaust_pipe_length_0(150.0) // cm
    label exhaust_volume_0(collector_cross_section_area * exhaust_pipe_length_0 / 100.0) // Litres
    
    label exhaust_pipe_length_1(150.0) // cm
    label exhaust_volume_1(collector_cross_section_area * exhaust_pipe_length_1 / 100.0) // Litres
    
    label spacing_factor(1.1)
    label flange_density(1.0)
    

    intake intake(
        plenum_volume: 5.242 * units.L,
        plenum_cross_section_area: 77.983 * units.cm2,
        runner_length: 14.776 * units.cm,
        intake_flow_rate: k_carb(intake_cfm),
        idle_flow_rate: k_carb(0.03),
        idle_throttle_plate_position: 0.9972,
        runner_flow_rate: k_carb(intake_cfm / (cyl / 2.0) * 1.5),
        velocity_decay: 0.05
    )
    
    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(exhaust_cfm / 1.5),
        collector_cross_section_area: collector_cross_section_area * units.cm2,
        length: exhaust_pipe_length_0 * units.cm,
        primary_tube_length: primary_tube_length * units.cm,
        primary_flow_rate: k_carb(exhaust_cfm / (cyl / 2.0) * 1.33),
        velocity_decay: 0.5,
        volume: (exhaust_volume_0 + 5.0) * units.L
    )
    
    exhaust_system_parameters es_params1(
        outlet_flow_rate: k_carb(exhaust_cfm / 1.5),
        collector_cross_section_area: collector_cross_section_area * units.cm2,
        length: exhaust_pipe_length_1 * units.cm,
        primary_tube_length: primary_tube_length * units.cm,
        primary_flow_rate: k_carb(exhaust_cfm / (cyl / 2.0) * 1.33),
        velocity_decay: 0.5,
        volume: (exhaust_volume_1 + 5.0) * units.L
    )

    exhaust_system exhaust0(es_params0, impulse_response: ir_lib.real_engine_0, audio_volume: 2.0)
    exhaust_system exhaust1(es_params1, impulse_response: ir_lib.default_0, audio_volume: 1.0)
 
    cylinder_bank b0(bank_params, angle: vee / 2)
    cylinder_bank b1(bank_params, angle: -vee / 2)

    label pl0((1.8 * bore * spacing_factor / flange_density) * units.mm)
    label pl1((2.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl2((1.8 * bore * spacing_factor / flange_density) * units.mm)
    label pl3((1.6 * bore * spacing_factor / flange_density) * units.mm)
    
    label pl4((1.8 * bore * spacing_factor / flange_density) * units.mm)
    label pl5((2.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl6((1.8 * bore * spacing_factor / flange_density) * units.mm)
    label pl7((1.6 * bore * spacing_factor / flange_density) * units.mm)

    b0
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl0,
            ignition_wire: wires.wire1
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl1,
            ignition_wire: wires.wire2
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl2,
            ignition_wire: wires.wire3
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl3,
            ignition_wire: wires.wire4
        )
        
    b1
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl4,
            ignition_wire: wires.wire5
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl5,
            ignition_wire: wires.wire6
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl6,
            ignition_wire: wires.wire7
        )
        .add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl7,
            ignition_wire: wires.wire8
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)

    engine.add_crankshaft(c0)

    camshaft_builder camshaft()
    
    engine.add_ignition_module(
        distributor(
            wires: wires,
            timing_curve: timing_curve
        )
    )

    b0.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1
        )
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 10 * units.deg)
        .add_sample(1000 * units.rpm, 15 * units.deg)
        .add_sample(2000 * units.rpm, 20 * units.deg)
        .add_sample(3000 * units.rpm, 25 * units.deg)
        .add_sample(4000 * units.rpm, 30 * units.deg)
        .add_sample(5000 * units.rpm, 33 * units.deg)
        .add_sample(6000 * units.rpm, 36 * units.deg)
        .add_sample(7000 * units.rpm, 38 * units.deg)
}

// Corvette C7 Convertible Z51
public node veh {
    alias output __out: vehicle;
    vehicle vehicle(
        mass: 1529 * units.kg,
        drag_coefficient: 0.30,
        cross_sectional_area: (1877 * units.mm) * (1235 * units.mm),
        diff_ratio: 3.42,
        tire_radius: (678.942 / 2) * units.mm, // P285/30R20
        rolling_resistance: 20
        )
}

// Z51
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 900 * units.Nm,
            max_clutch_flex: 20 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 10 * units.Nm / units.deg,
            clutch_damping: 1.0,
            simulate_flex: true
        )
        .add_gear(2.97)
        .add_gear(2.07)
        .add_gear(1.43)
        .add_gear(1.0)
        .add_gear(0.71)
        .add_gear(0.57)
        .add_gear(0.48);
}

run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)