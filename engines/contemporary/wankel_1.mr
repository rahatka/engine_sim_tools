import "engine_sim.mr"
import "../../fuel/90.mr"

units units()
constants constants()

// Engine Sim V0.1.14A
// 1-rotor
// wankel_1
// Created by oror 2023
// 1

label rotor_sides(3.0)
label rotors(1.0)
label rotor_initial_angle(60.0 * units.deg)

label rot360(360.0 * units.deg)
label cycle(rotor_sides * 360.0 * units.deg)

label intake_port_center(43.0) // + degrees relative to TDC axis
label exhaust_port_center(50.0) // - degrees relative to TDC axis

label intake_port_duration(23.0) // degrees
label exhaust_port_duration(22.0) // degrees

label stroke(3.0 * units.cm)
label radius(10.5 * units.cm)
label depth(8.0) // units.cm
label depression_volume(26.0 * units.cc) // 9.7 CR
label intake_port_start((180.0 - intake_port_center - intake_port_duration / 2) * units.deg)
label intake_port_end((180.0 - intake_port_center + intake_port_duration / 2) * units.deg)
label exhaust_port_start((180.0 + exhaust_port_center - exhaust_port_duration / 2) * units.deg)
label exhaust_port_end((180.0 + exhaust_port_center + exhaust_port_duration / 2) * units.deg)
label rotor_mass(4328 * units.g)
label rotor_I(disk_moment_of_inertia(mass: rotor_mass, radius: radius))
label intake_runner_area(intake_port_duration / 33.3 * (depth * 0.9)) // units.cm2
label exhaust_runner_area(exhaust_port_duration / 33.3 * (depth * 0.9)) // units.cm2
label intake_flow(k_carb(intake_runner_area * 75))
label exhaust_flow(k_carb(exhaust_runner_area * 75))

label ci(40.0)
label redline(7500.0)
label intake_cfm((ci * redline * 0.75) / 3456.0)
label exhaust_cfm((redline * 0.001) * ci * 0.75)

private node wires {
    output wire1: ignition_wire();
}

private node eng {
    alias output __out: engine;

    wankel_engine engine(
        name: "Mazda 6b? 1-rotor",
        starter_torque: 100 * units.Nm,
        starter_speed: 1000 * units.rpm,
        redline: redline * units.rpm,
        fuel: fuel(
            max_burning_efficiency: 0.95,
            max_dilution_effect: 1.5,
            burning_efficiency_randomness: 1.0
        ),
        throttle_gamma: 1.7,
        hf_gain: 0.01,
        noise: 0.2,
        jitter: 0.2,
        simulation_frequency: 16000,
        rotor_calculation_quality: 1024,
        rotor_housing_resolution: 1024
    )

    wires wires()

    crankshaft c0(
        throw: 0.5 * stroke,
        flywheel_mass: 2 * units.kg,
        mass: 4 * units.kg,
        friction_torque: 20.0 * units.Nm,
        moment_of_inertia: 0.08,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0 * units.deg
    )

    rod_journal rj0(angle: (0.0 / rotors) * rot360)

    c0
        .add_rod_journal(rj0)

    engine.add_crankshaft(c0)

    wankel_rotor rotor0(
        mass: rotor_mass,
        moment_of_inertia: rotor_I,
        radius: radius,
        crown_gear_radius: 3 * 0.5 * stroke,
        angle: rotor_initial_angle + (0.0 / (rotors * rotor_sides)) * rot360,
        depth: depth * units.cm,
        depression_volume: depression_volume
    )

    intake intake(
        plenum_volume: 1.5 * units.L,
        plenum_cross_section_area: (intake_runner_area * rotors / 2) * units.cm2,
        intake_flow_rate: k_carb(intake_cfm),
        idle_flow_rate: k_carb(0.20),
        idle_throttle_plate_position: 0.989,
        runner_flow_rate: k_carb(intake_cfm / rotors),
        runner_length: 10.0 * units.cm,
        velocity_decay: 0.05
    )

    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(exhaust_cfm),
        primary_tube_length: 50.0 * units.cm,
        collector_cross_section_area: (exhaust_runner_area * rotors / 2) * units.cm2,
        primary_flow_rate: k_carb(exhaust_cfm / rotors),
        velocity_decay: 1.2,
        length: 150.0 * units.cm,
        backflow_atmospheric_mixing: 0.5,
        volume: 3.0 * units.L
    )
 
    // exhaust_system exhaust0(es_params0, audio_volume: 0.05, impulse_response: impulse_response(filename: "E:/smooth_m.wav", volume: 0.001))
    exhaust_system exhaust0(es_params0, audio_volume: 1.0, impulse_response: ir_lib.minimal_muffling_01)
    
    label div(1.2)
    label pl0 (0.0 * (depth * units.cm) / div)

    engine.add_rotor(
        intake,
        exhaust0,
        rotor0,
        rj0,
        wires.wire1,
        primary_length: pl0,
        intake_port: default_wankel_peripheral_port(intake_port_start, intake_port_end, intake_flow),
        exhaust_port: default_wankel_peripheral_port(exhaust_port_start, exhaust_port_end, exhaust_flow),
        intake_runner_cross_section_area: intake_runner_area * units.cm2,
        exhaust_cross_section_area: exhaust_runner_area * units.cm2,
        spark_plug_angle: 0
    )

    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 10 * units.deg)
        .add_sample(1000 * units.rpm, 15 * units.deg)
        .add_sample(2000 * units.rpm, 20 * units.deg)
        .add_sample(3000 * units.rpm, 23 * units.deg)
        .add_sample(4000 * units.rpm, 24 * units.deg)
        .add_sample(5000 * units.rpm, 25 * units.deg)
        .add_sample(6000 * units.rpm, 26 * units.deg)
        .add_sample(7000 * units.rpm, 27 * units.deg)
        .add_sample(8000 * units.rpm, 28 * units.deg)
        .add_sample(9000 * units.rpm, 29 * units.deg)
        .add_sample(10000 * units.rpm, 30 * units.deg)

    engine.add_ignition_module(
        ignition_module(timing_curve: timing_curve, rev_limit: 8000 * units.rpm, limiter_duration: 0.03)
            .connect_wire(wires.wire1, (0.0 / (rotors * rotor_sides)) * cycle)

            .connect_wire(wires.wire1, (2.0 / (rotors * rotor_sides)) * cycle)

            .connect_wire(wires.wire1, (4.0 / (rotors * rotor_sides)) * cycle)
    )
}

// 93-95 RX7 Turbo
public node veh {
    alias output __out: vehicle;
    vehicle vehicle(
        mass: 1284  * units.kg,
        drag_coefficient: 0.33,
        cross_sectional_area: (1750  * units.mm) * (1230 * units.mm),
        diff_ratio: 4.1,
        tire_radius: (632.0 / 2) * units.mm, // 225/50ZR16
        rolling_resistance: 20,
        damping: 15.0,
        max_flex: 5 * units.deg,
        limit_flex: true,
        simulate_flex: true
        )
}

// 93-95 RX7 Turbo
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 200 * units.Nm,
            max_clutch_flex: 10 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 50 * units.Nm / units.deg,
            clutch_damping: 2.0,
            simulate_flex: true
        )
        .add_gear(3.483)
        .add_gear(2.015)
        .add_gear(1.391)
        .add_gear(1.0)
        .add_gear(0.719);
}

run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)
