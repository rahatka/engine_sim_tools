import "engine_sim.mr"
import "_fuel_30.mr"

units units()
constants constants()

// Engine Sim V0.1.14A
// VW W8
// vw_w8
// Created by oror 2023
// 1-5-2-6-4-8-3-7

label bore(84.0)
label stroke(90.2)
label compression_ratio(10.7)
label con_rod(164.0)
label compression_height(35.0 * units.mm) // ?
label intake_valve_diameter(31.0)
label exhaust_valve_diameter(27.0)
label intake_valves(2.0)
label exhaust_valves(2.0)

label volume(3.999) // L; port_flow.py v0.6
label redline(6500.0)

label cyl(8.0)
label row(2.0)
label cycle(720.0 * units.deg)
label vee(72.0 * units.deg)
label vree(15.0 * units.deg)
label rot(cycle / cyl)
label rot90(90.0 * units.deg)
label rot180(180.0 * units.deg)
label rot360(360.0 * units.deg)

label phase_shift(cycle / cyl - vee)

// VW VR6 268Â° Sportnockenwellen
label IVL(11.4)
label EVL(11.4)
label IVO(19.0 * units.deg) //BTDC
label IVC(69.0 * units.deg) //ABDC
label EVO(69.0 * units.deg) //BBDC
label EVC(19.0 * units.deg) //ATDC

label intake_duration(IVO + IVC + rot180)
label exhaust_duration(EVO + EVC + rot180)

label ILC((rot180 - IVO + IVC) / 2.0 + rot360)
label ELC((rot180 - EVO + EVC) / 2.0 + rot180)

label exhaust_delay_coeff(1.5)

public node eng_distributor_even {
    input wires;
    input timing_curve;
    input rev_limit: 7000 * units.rpm;
    input limiter_duration: 0.06;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, rot * 0)
            .connect_wire(wires.wire2, rot * 2)
            .connect_wire(wires.wire4, rot * 4)
            .connect_wire(wires.wire3, rot * 6)
            
            .connect_wire(wires.wire5, rot * 1)
            .connect_wire(wires.wire6, rot * 3)
            .connect_wire(wires.wire8, rot * 5)
            .connect_wire(wires.wire7, rot * 7);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
}

public node intake_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: intake_duration,
            gamma: 4.0,
            lift: IVL * units.mm,
            steps: 100
        );
}

public node exhaust_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: exhaust_duration,
            gamma: 4.0,
            lift: EVL * units.mm,
            steps: 100
        );
}

// even firing camshaft
public node camshaft_builder_even {
    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;
    output intake_cam_2: _intake_cam_2;
    output exhaust_cam_2: _exhaust_cam_2;
    output intake_cam_3: _intake_cam_3;
    output exhaust_cam_3: _exhaust_cam_3;

    camshaft _intake_cam_0(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_1(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_2(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_2(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_3(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_3(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)

    _intake_cam_0
        .add_lobe(ILC + 0 * rot) // 1
        .add_lobe(ILC + 6 * rot) // 3
    _exhaust_cam_0
        .add_lobe(ELC + 0 * rot)
        .add_lobe(ELC + 6 * rot)
        
    _intake_cam_1
        .add_lobe(ILC + 2 * rot) // 2
        .add_lobe(ILC + 4 * rot) // 4
    _exhaust_cam_1
        .add_lobe(ELC + 2 * rot)
        .add_lobe(ELC + 4 * rot)

    _intake_cam_2
        .add_lobe(ILC + 1 * rot) // 5
        .add_lobe(ILC + 7 * rot) // 7
    _exhaust_cam_2
        .add_lobe(ELC + 1 * rot)
        .add_lobe(ELC + 7 * rot)
        
    _intake_cam_3
        .add_lobe(ILC + 3 * rot) // 6
        .add_lobe(ILC + 5 * rot) // 8
    _exhaust_cam_3
        .add_lobe(ELC + 3 * rot)
        .add_lobe(ELC + 5 * rot)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node head { // generated by port_flow.py v0.6
    // intake H: 31.0 L: 11.4 N: 2.0
    // exhaust H: 27.0 L: 11.4 N: 2.0
    // intake port area: 12.8 cm2; effective lift: 6.7 mm
    // exhaust port area: 9.7 cm2; effective lift: 5.9 mm
    // cylinder volume: 499.9 cc; engine volume: 3.999 L; 244.0 CI
    // 16 harmonic intake runner length: 14.6 cm; diameter: 4.1 cm
    // primary length: 49.9 cm, area: 8.6 cm2, diameter: 3.3 cm
    // collector diameter: 6.6 cm, area: 34.5 cm2
    // target power: 5850 RPM, torque: 3861 RPM

    input intake_camshaft;
    input exhaust_camshaft;
    input flip_display: false;
    
    alias output __out: head;
    function intake_flow(1.140 * units.mm)
    intake_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.140, 45.0)
        .add_flow_sample(2.280, 77.5)
        .add_flow_sample(3.420, 105.3)
        .add_flow_sample(4.560, 130.0)
        .add_flow_sample(5.700, 152.5)
        .add_flow_sample(6.840, 173.4)
        .add_flow_sample(7.980, 174.1)
        .add_flow_sample(9.120, 174.8)
        .add_flow_sample(10.260, 175.5)
        .add_flow_sample(11.400, 176.3)
        
    function exhaust_flow(1.140 * units.mm)
    exhaust_flow
        .add_flow_sample(0.0, 0.0)
        .add_flow_sample(1.140, 38.1)
        .add_flow_sample(2.280, 65.3)
        .add_flow_sample(3.420, 88.5)
        .add_flow_sample(4.560, 108.9)
        .add_flow_sample(5.700, 127.4)
        .add_flow_sample(6.840, 131.7)
        .add_flow_sample(7.980, 132.2)
        .add_flow_sample(9.120, 132.8)
        .add_flow_sample(10.260, 133.4)
        .add_flow_sample(11.400, 134.1)
        
    generic_cylinder_head head(
        chamber_volume: 46.717 * units.cc,
        intake_runner_volume: 190.1 * units.cc,
        intake_runner_cross_section_area: 13.1 * units.cm2,
        exhaust_runner_volume: 63.4 * units.cc,
        exhaust_runner_cross_section_area: 8.6 * units.cm2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: standard_valvetrain(
            intake_camshaft: intake_camshaft,
            exhaust_camshaft: exhaust_camshaft
        ),
        flip_display: flip_display
    )
}

public node eng {
    alias output __out: engine;

    engine engine(
        name: "VAG 4.0 W8",
        starter_torque: 100 *units.Nm,
        starter_speed: 650 * units.rpm,
        redline: redline * units.rpm,
        fuel: fuel(
            max_turbulence_effect: 3.0,
            burning_efficiency_randomness: 0.2,
            max_burning_efficiency: 0.99
        ),
        throttle_gamma: 1.5,
        jitter: 0.7,
        noise: 0.1,
        hf_gain: 0.003,
        simulation_frequency: 10000
    )

    wires wires()
    
    connecting_rod_parameters cr_params(
        mass: 750.0 * units.g,
        moment_of_inertia: 0.005,
        center_of_mass: 0.0,
        length: con_rod * units.mm
    )

    crankshaft c0(
        throw: (stroke / 2.0) * units.mm,
        flywheel_mass: 6 * units.kg,
        mass: 12 * units.kg,
        friction_torque: 5.0 * units.Nm,
        moment_of_inertia: 0.2,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0
    )

    // even firing crank
    rod_journal rj0_0(angle: rot * 0 + rot90 + vee / 2 + vree / 2)
    rod_journal rj0_1(angle: rot * 0 + rot90 + vee / 2 + vree / 2 + phase_shift)
    rod_journal rj1_0(angle: rot * 6 + rot90 + vee / 2 - vree / 2)
    rod_journal rj1_1(angle: rot * 6 + rot90 + vee / 2 - vree / 2 + phase_shift)
    rod_journal rj2_0(angle: rot * 2 + rot90 + vee / 2 + vree / 2)
    rod_journal rj2_1(angle: rot * 2 + rot90 + vee / 2 + vree / 2 + phase_shift)
    rod_journal rj3_0(angle: rot * 4 + rot90 + vee / 2 - vree / 2)
    rod_journal rj3_1(angle: rot * 4 + rot90 + vee / 2 - vree / 2 + phase_shift)
    
    c0
        .add_rod_journal(rj0_0)
        .add_rod_journal(rj0_1)
        .add_rod_journal(rj1_0)
        .add_rod_journal(rj1_1)
        .add_rod_journal(rj2_0)
        .add_rod_journal(rj2_1)
        .add_rod_journal(rj3_0)
        .add_rod_journal(rj3_1)

    piston_parameters piston_params(
        mass: 750 * units.g,
        blowby: k_28inH2O(0.1),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    cylinder_bank_parameters bank_params(
        bore: bore * units.mm,
        deck_height: (con_rod + stroke / 2.0) * units.mm + compression_height
    )

    label intake_valve_circ(intake_valve_diameter * constants.pi)
    label intake_valve_area(intake_valve_circ * IVL / 100.0) // cm2

    label intake_plenum_area(intake_valve_area * cyl * intake_valves) // cm2
    label collector_cross_section_area(17.2) // port_flow.py v0.6

    label exhaust_pipe_length_0(250.0) // cm
    label exhaust_volume_0(collector_cross_section_area * exhaust_pipe_length_0 / 100.0) // Litres
    
    label exhaust_pipe_length_1(252.0) // cm
    label exhaust_volume_1(collector_cross_section_area * exhaust_pipe_length_1 / 100.0) // Litres
    
    label spacing_factor(0.75)
    label flange_density(1.0 * exhaust_delay_coeff)
    

    intake intake(
        plenum_volume: 3.5 * units.L, // port_flow.py v0.6
        plenum_cross_section_area: 52.2 * units.cm2, // port_flow.py v0.6
        intake_flow_rate: k_carb(433.2), // port_flow.py v0.6
        idle_flow_rate: k_carb(0.03),
        idle_throttle_plate_position: 0.996,
        runner_flow_rate: k_carb(108.3), // PF 1.0; port_flow.py v0.6
        runner_length: 14.6 * units.cm, // port_flow.py v0.6
        velocity_decay: 0.05
    )
    
    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(361.0), // port_flow.py v0.6
        collector_cross_section_area: 25.8 * units.cm2, // CF 1.5; port_flow.py v0.6
        length: exhaust_pipe_length_0 * units.cm,
        primary_tube_length: 49.9 * units.cm, // port_flow.py v0.6
        primary_flow_rate: k_carb(180.5), // PF 1.0; port_flow.py v0.6
        velocity_decay: 1.0,
        volume: (exhaust_volume_0 + 3.0) * units.L
    )
    
    exhaust_system_parameters es_params1(
        outlet_flow_rate: k_carb(361.0), // port_flow.py v0.6
        collector_cross_section_area: 25.8 * units.cm2, // CF 1.5; port_flow.py v0.6
        length: exhaust_pipe_length_1 * units.cm,
        primary_tube_length: 49.9 * units.cm, // port_flow.py v0.6
        primary_flow_rate: k_carb(180.5), // PF 1.0; port_flow.py v0.6
        velocity_decay: 1.0,
        volume: (exhaust_volume_1 + 3.0) * units.L
    )

    // exhaust_system exhaust0(es_params0, audio_volume: 0.3, impulse_response: impulse_response(filename: "C:/list/112/assets/sound-library/smooth/smooth_11.wav", volume: 0.001))
    // exhaust_system exhaust1(es_params1, audio_volume: 0.3, impulse_response: impulse_response(filename: "C:/list/112/assets/sound-library/smooth/smooth_11.wav", volume: 0.001))
    exhaust_system exhaust0(es_params0, impulse_response: ir_lib.default_0, audio_volume: 10.0)
    exhaust_system exhaust1(es_params1, impulse_response: ir_lib.default_0, audio_volume: 10.0)
 
    // cylinder_bank b0(bank_params, angle: vee / 2 + vree / 2, position_x: -8.60443 * units.mm, position_y: -9.06718 * units.mm)
    // cylinder_bank b1(bank_params, angle: vee / 2 - vree / 2, position_x: 10.98521 * units.mm, position_y: 5.96448 * units.mm)
    // cylinder_bank b2(bank_params, angle: -vee / 2 + vree / 2, position_x: -10.98521 * units.mm, position_y: 5.96448 * units.mm)
    // cylinder_bank b3(bank_params, angle: -vee / 2 - vree / 2, position_x: 8.60443 * units.mm, position_y: -9.06718 * units.mm)
    
    cylinder_bank b0(bank_params, angle: vee / 2 + vree / 2)
    cylinder_bank b1(bank_params, angle: vee / 2 - vree / 2)
    cylinder_bank b2(bank_params, angle: -vee / 2 + vree / 2)
    cylinder_bank b3(bank_params, angle: -vee / 2 - vree / 2)
    
    label pl0 ((4.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl1 ((3.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl2 ((2.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl3 ((1.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    
    label pl4 ((4.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl5 ((3.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl6 ((2.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl7 ((1.0 * bore * spacing_factor / flange_density) * units.mm)

    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl0,
            ignition_wire: wires.wire1
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl1,
            ignition_wire: wires.wire2
        )
    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl2,
            ignition_wire: wires.wire3
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl3,
            ignition_wire: wires.wire4
        )
        
    b2.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl4,
            ignition_wire: wires.wire5
        )
    b3.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl5,
            ignition_wire: wires.wire6
        )
    b2.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl6,
            ignition_wire: wires.wire7
        )
    b3.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl7,
            ignition_wire: wires.wire8
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)
        .add_cylinder_bank(b2)
        .add_cylinder_bank(b3)

    engine.add_crankshaft(c0)

    camshaft_builder_even camshaft()

    b0.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            flip_display: true
        )
    )
    
    b2.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_2,
            exhaust_camshaft: camshaft.exhaust_cam_2
        )
    )
    b3.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_3,
            exhaust_camshaft: camshaft.exhaust_cam_3
        )
    )

    // ignition_timing.py v0.1 r=1.21
    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 10.0 * units.deg)
        .add_sample(1000 * units.rpm, 12.1 * units.deg)
        .add_sample(2000 * units.rpm, 14.6 * units.deg)
        .add_sample(3000 * units.rpm, 17.7 * units.deg)
        .add_sample(4000 * units.rpm, 21.4 * units.deg)
        .add_sample(5000 * units.rpm, 25.9 * units.deg)
        .add_sample(6000 * units.rpm, 31.4 * units.deg)
        .add_sample(7000 * units.rpm, 38.0 * units.deg)

    engine.add_ignition_module(
        eng_distributor_even(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

 // Passat W8
public node veh {
    alias output __out: vehicle;
    vehicle vehicle(
        mass: 1664 * units.kg,
        drag_coefficient: 0.27,
        cross_sectional_area: (1746 * units.mm) * (1461 * units.mm),
        diff_ratio: 4.11,
        tire_radius: (634.0 / 2) * units.mm, // 225/45R17
        rolling_resistance: 25
        )
}

 // Passat W8
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 500 * units.Nm,
            max_clutch_flex: 20 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 15 * units.Nm / units.deg,
            clutch_damping: 0.5,
            simulate_flex: true
        )
        .add_gear(3.50)
        .add_gear(1.89)
        .add_gear(1.23)
        .add_gear(0.97)
        .add_gear(0.81)
        .add_gear(0.68);
}

run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)
