// Engine Sim V0.1.14A
// VW W12 420 @ 6200
// Created by oror 2023
// 1-12-5-8-3-10-6-7-2-11-4-9

/*
{
  "engine": {
    "catalog_id": 221,
    "description": [
      "This is my attempt to recreate this piece of german engineering.",
      "VAG W12 is an even firing engine, meaning each powerstroke happening with 60° interval despite 72° Vee and 15° VR angle.",
      "This is possible by using a complex split pin crankshaft which is a piece of art itself. Google it.",
      "I tried to parametrize this engine, so it actually has a definition of one VR bank and the second bank is a copy of the first one but rotated by 360+72 deg.",
      "1-12-5-8-3-10-6-7-2-11-4-9"
    ],
    "enable_update": true,
    "es_version": "V0.1.14A",
    "name": "VW 6.0 W12 (Volkswagen Phaeton) 2003",
    "yt_handle": "jsnBuxwYLmU"
  },
  "flow_cfg": {
    "collector_area_coeff": 0.45,
    "exhaust_flange_dia": null,
    "exhaust_port_dia": null,
    "exhaust_stem_dia": null,
    "intake_port_dia": null,
    "intake_runner_dia_mult": 1.0,
    "intake_stem_dia": null,
    "ir_to_er_ratio": 3.0,
    "max_power_rpm": 6200.0,
    "max_torque_rpm": 3000.0,
    "port_to_valve_area": 0.8,
    "power_factor": 1.1,
    "primary_area_coeff": 1.1,
    "resolution": 32,
    "smoothness": 3.3,
    "valve_to_stem_dia": 5.0
  },
  "ign_cfg": {
    "end_deg": 40,
    "exp_mode": 2,
    "flywheel": 0,
    "resolution": 16,
    "start_rpm": 600
  }
}
*/

import "engine_sim.mr"
import "../../fuel/90.mr"

units units()
constants constants()

label bore(84.0)
label stroke(90.2)
label compression_ratio(10.7)
label con_rod(164.0)
label compression_height(35.0 * units.mm) // ?
label intake_valve_diameter(31.0)
label exhaust_valve_diameter(27.0)
label intake_valves(2.0)
label exhaust_valves(2.0)

label redline(6500.0) // RPM

label cyl(12.0)
label row(2.0)
label cycle(720.0 * units.deg)
label vee(72.0 * units.deg)
label vree(15.0 * units.deg)
label rot(cycle / cyl)
label rot90(90.0 * units.deg)
label rot180(180.0 * units.deg)
label rot360(360.0 * units.deg)
label phase_shift(cycle / cyl - vee)

// A8L advance
label IVL(10.0)
label EVL(10.0)
label IVO(27.0 * units.deg) //BTDC
label IVC(3.0 * units.deg) //ABDC
label EVO(55.0 * units.deg) //BBDC
label EVC(20.0 * units.deg) //ATDC

label intake_duration(IVO + IVC + rot180)
label exhaust_duration(EVO + EVC + rot180)

label ILC((rot180 - IVO + IVC) / 2.0 + rot360)
label ELC((rot180 - EVO + EVC) / 2.0 + rot180)

// A8L retard
label V_IVO(25.0 * units.deg) //BTDC
label V_IVC(55.0 * units.deg) //ABDC
label V_EVO(33.0 * units.deg) //BBDC
label V_EVC(2.0 * units.deg) //ATDC

label v_intake_duration(V_IVO + V_IVC + rot180)
label v_exhaust_duration(V_EVO + V_EVC + rot180)

label V_ILC((-V_IVO + rot180 + V_IVC) / 2.0 + rot360)
label V_ELC((rot180 - V_EVO + V_EVC + rot360) / 2.0)

label con_rod_mass(550.0) // ? g
label crank_mass(28.0) // ? kg
label flywheel_mass(8.0) // kg

label exhaust_delay_coeff(1.5)

public node eng_distributor_even {
    input wires;
    input timing_curve;
    input rev_limit: 7000 * units.rpm;
    input limiter_duration: 0.06;
    alias output __out:
        ignition_module(timing_curve: timing_curve, rev_limit: rev_limit, limiter_duration: limiter_duration)
            .connect_wire(wires.wire1, rot * 0)
            .connect_wire(wires.wire5, rot * 2)
            .connect_wire(wires.wire3, rot * 4)
            .connect_wire(wires.wire6, rot * 6)
            .connect_wire(wires.wire2, rot * 8)
            .connect_wire(wires.wire4, rot * 10)
            
            .connect_wire(wires.wire7, rot * 1 + rot360)
            .connect_wire(wires.wire11, rot * 3 + rot360)
            .connect_wire(wires.wire9, rot * 5 + rot360)
            .connect_wire(wires.wire12, rot * 7 + rot360)
            .connect_wire(wires.wire8, rot * 9 + rot360)
            .connect_wire(wires.wire10, rot * 11 + rot360);
}

private node wires {
    output wire1: ignition_wire();
    output wire2: ignition_wire();
    output wire3: ignition_wire();
    output wire4: ignition_wire();
    output wire5: ignition_wire();
    output wire6: ignition_wire();
    output wire7: ignition_wire();
    output wire8: ignition_wire();
    output wire9: ignition_wire();
    output wire10: ignition_wire();
    output wire11: ignition_wire();
    output wire12: ignition_wire();
}

public node intake_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: intake_duration,
            gamma: 3.0,
            lift: IVL * units.mm,
            steps: 100
        );
}

public node exhaust_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: exhaust_duration,
            gamma: 3.0,
            lift: EVL * units.mm,
            steps: 100
        );
}

public node v_intake_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: v_intake_duration,
            gamma: 3.0,
            lift: IVL * units.mm,
            steps: 100
        );
}

public node v_exhaust_lobe_profile {
    alias output __out:
        harmonic_cam_lobe(
            duration_at_50_thou: v_exhaust_duration,
            gamma: 3.0,
            lift: EVL * units.mm,
            steps: 100
        );
}

public node intake_flow {
    alias output __out: _intake_flow;
    function _intake_flow(0.323 * units.mm)
    _intake_flow
        .add_flow_sample(0.000, 0.0)
        .add_flow_sample(0.323, 11.9)
        .add_flow_sample(0.645, 23.5)
        .add_flow_sample(0.968, 34.8)
        .add_flow_sample(1.290, 45.4)
        .add_flow_sample(1.613, 55.5)
        .add_flow_sample(1.935, 64.9)
        .add_flow_sample(2.258, 73.8)
        .add_flow_sample(2.581, 82.2)
        .add_flow_sample(2.903, 90.2)
        .add_flow_sample(3.226, 97.8)
        .add_flow_sample(3.548, 105.1)
        .add_flow_sample(3.871, 112.1)
        .add_flow_sample(4.194, 118.9)
        .add_flow_sample(4.516, 125.3)
        .add_flow_sample(4.839, 131.5)
        .add_flow_sample(5.161, 137.4)
        .add_flow_sample(5.484, 142.9)
        .add_flow_sample(5.806, 147.9)
        .add_flow_sample(6.129, 152.4)
        .add_flow_sample(6.452, 156.2)
        .add_flow_sample(6.774, 159.4)
        .add_flow_sample(7.097, 161.8)
        .add_flow_sample(7.419, 163.7)
        .add_flow_sample(7.742, 165.0)
        .add_flow_sample(8.065, 165.9)
        .add_flow_sample(8.387, 166.5)
        .add_flow_sample(8.710, 166.9)
        .add_flow_sample(9.032, 167.2)
        .add_flow_sample(9.355, 167.4)
        .add_flow_sample(9.677, 167.6)
        .add_flow_sample(10.000, 167.7)
}

public node exhaust_flow {
    alias output __out: _exhaust_flow;
    function _exhaust_flow(0.323 * units.mm)
    _exhaust_flow
        .add_flow_sample(0.000, 0.0)
        .add_flow_sample(0.323, 11.7)
        .add_flow_sample(0.645, 23.2)
        .add_flow_sample(0.968, 34.2)
        .add_flow_sample(1.290, 44.7)
        .add_flow_sample(1.613, 54.5)
        .add_flow_sample(1.935, 63.7)
        .add_flow_sample(2.258, 72.4)
        .add_flow_sample(2.581, 80.5)
        .add_flow_sample(2.903, 88.3)
        .add_flow_sample(3.226, 95.6)
        .add_flow_sample(3.548, 102.6)
        .add_flow_sample(3.871, 109.2)
        .add_flow_sample(4.194, 115.5)
        .add_flow_sample(4.516, 121.3)
        .add_flow_sample(4.839, 126.7)
        .add_flow_sample(5.161, 131.5)
        .add_flow_sample(5.484, 135.6)
        .add_flow_sample(5.806, 139.0)
        .add_flow_sample(6.129, 141.8)
        .add_flow_sample(6.452, 143.8)
        .add_flow_sample(6.774, 145.3)
        .add_flow_sample(7.097, 146.4)
        .add_flow_sample(7.419, 147.1)
        .add_flow_sample(7.742, 147.5)
        .add_flow_sample(8.065, 147.8)
        .add_flow_sample(8.387, 148.1)
        .add_flow_sample(8.710, 148.3)
        .add_flow_sample(9.032, 148.4)
        .add_flow_sample(9.355, 148.6)
        .add_flow_sample(9.677, 148.8)
        .add_flow_sample(10.000, 148.9)
}

intake_lobe_profile intake_lobe_profile()
exhaust_lobe_profile exhaust_lobe_profile()

intake_flow intake_flow()
exhaust_flow exhaust_flow()

v_intake_lobe_profile v_intake_lobe_profile()
v_exhaust_lobe_profile v_exhaust_lobe_profile()

public node camshaft_builder_even {
    output intake_cam_0: _intake_cam_0;
    output exhaust_cam_0: _exhaust_cam_0;
    output intake_cam_1: _intake_cam_1;
    output exhaust_cam_1: _exhaust_cam_1;
    output intake_cam_2: _intake_cam_2;
    output exhaust_cam_2: _exhaust_cam_2;
    output intake_cam_3: _intake_cam_3;
    output exhaust_cam_3: _exhaust_cam_3;

    output v_intake_cam_0: _v_intake_cam_0;
    output v_exhaust_cam_0: _v_exhaust_cam_0;
    output v_intake_cam_1: _v_intake_cam_1;
    output v_exhaust_cam_1: _v_exhaust_cam_1;
    output v_intake_cam_2: _v_intake_cam_2;
    output v_exhaust_cam_2: _v_exhaust_cam_2;
    output v_intake_cam_3: _v_intake_cam_3;
    output v_exhaust_cam_3: _v_exhaust_cam_3;

    camshaft _intake_cam_0(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_0(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_1(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_1(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_2(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_2(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)
    camshaft _intake_cam_3(base_radius: IVL * 1.5 * units.mm, lobe_profile: intake_lobe_profile)
    camshaft _exhaust_cam_3(base_radius: EVL * 1.5 * units.mm, lobe_profile: exhaust_lobe_profile)

    camshaft _v_intake_cam_0(base_radius: IVL * 1.5 * units.mm, lobe_profile: v_intake_lobe_profile)
    camshaft _v_exhaust_cam_0(base_radius: EVL * 1.5 * units.mm, lobe_profile: v_exhaust_lobe_profile)
    camshaft _v_intake_cam_1(base_radius: IVL * 1.5 * units.mm, lobe_profile: v_intake_lobe_profile)
    camshaft _v_exhaust_cam_1(base_radius: EVL * 1.5 * units.mm, lobe_profile: v_exhaust_lobe_profile)
    camshaft _v_intake_cam_2(base_radius: IVL * 1.5 * units.mm, lobe_profile: v_intake_lobe_profile)
    camshaft _v_exhaust_cam_2(base_radius: EVL * 1.5 * units.mm, lobe_profile: v_exhaust_lobe_profile)
    camshaft _v_intake_cam_3(base_radius: IVL * 1.5 * units.mm, lobe_profile: v_intake_lobe_profile)
    camshaft _v_exhaust_cam_3(base_radius: EVL * 1.5 * units.mm, lobe_profile: v_exhaust_lobe_profile)

    _intake_cam_0
        .add_lobe(ILC + 0 * rot) // 1
        .add_lobe(ILC + 4 * rot) // 3
        .add_lobe(ILC + 2 * rot) // 5
    _exhaust_cam_0
        .add_lobe(ELC + 0 * rot)
        .add_lobe(ELC + 4 * rot)
        .add_lobe(ELC + 2 * rot)
        
    _intake_cam_1
        .add_lobe(ILC + 8 * rot) // 2
        .add_lobe(ILC + 10 * rot) // 4
        .add_lobe(ILC + 6 * rot) // 6
    _exhaust_cam_1
        .add_lobe(ELC + 8 * rot)
        .add_lobe(ELC + 10 * rot)
        .add_lobe(ELC + 6 * rot)

    _intake_cam_2
        .add_lobe(ILC + 1 * rot + rot360) // 7
        .add_lobe(ILC + 5 * rot + rot360) // 9
        .add_lobe(ILC + 3 * rot + rot360) // 11
    _exhaust_cam_2
        .add_lobe(ELC + 1 * rot + rot360)
        .add_lobe(ELC + 5 * rot + rot360)
        .add_lobe(ELC + 3 * rot + rot360)
        
    _intake_cam_3
        .add_lobe(ILC + 9 * rot + rot360) // 8
        .add_lobe(ILC + 11 * rot + rot360) // 10
        .add_lobe(ILC + 7 * rot + rot360) // 12
    _exhaust_cam_3
        .add_lobe(ELC + 9 * rot + rot360)
        .add_lobe(ELC + 11 * rot + rot360)
        .add_lobe(ELC + 7 * rot + rot360)

    _v_intake_cam_0
        .add_lobe(V_ILC + 0 * rot) // 1
        .add_lobe(V_ILC + 4 * rot) // 3
        .add_lobe(V_ILC + 2 * rot) // 5
    _v_exhaust_cam_0
        .add_lobe(V_ELC + 0 * rot)
        .add_lobe(V_ELC + 4 * rot)
        .add_lobe(V_ELC + 2 * rot)
        
    _v_intake_cam_1
        .add_lobe(V_ILC + 8 * rot) // 2
        .add_lobe(V_ILC + 10 * rot) // 4
        .add_lobe(V_ILC + 6 * rot) // 6
    _v_exhaust_cam_1
        .add_lobe(V_ELC + 8 * rot)
        .add_lobe(V_ELC + 10 * rot)
        .add_lobe(V_ELC + 6 * rot)

    _v_intake_cam_2
        .add_lobe(V_ILC + 1 * rot + rot360) // 7
        .add_lobe(V_ILC + 5 * rot + rot360) // 9
        .add_lobe(V_ILC + 3 * rot + rot360) // 11
    _v_exhaust_cam_2
        .add_lobe(V_ELC + 1 * rot + rot360)
        .add_lobe(V_ELC + 5 * rot + rot360)
        .add_lobe(V_ELC + 3 * rot + rot360)
        
    _v_intake_cam_3
        .add_lobe(V_ILC + 9 * rot + rot360) // 8
        .add_lobe(V_ILC + 11 * rot + rot360) // 10
        .add_lobe(V_ILC + 7 * rot + rot360) // 12
    _v_exhaust_cam_3
        .add_lobe(V_ELC + 9 * rot + rot360)
        .add_lobe(V_ELC + 11 * rot + rot360)
        .add_lobe(V_ELC + 7 * rot + rot360)
}

private node add_flow_sample {
    input lift;
    input flow;
    input this;
    alias output __out: this;

    this.add_sample(lift * units.mm, k_28inH2O(flow))
}

public node head {
    // port_flow.py v1.5
    // intake port area: 11.5 cm²; saturated lift: 6.59 mm
    // exhaust port area: 8.7 cm²; saturated lift: 5.74 mm
    // cylinder volume: 499.9 cm³ (30.50 CI); engine volume: 5.998 L (366.05 CI)
    // chamber volume: 51.5 cm³ (3.14 CI)
    // 16 harmonic intake runner length: 15.5 cm; diameter: 4.2 cm
    // primary length: 24.6 cm, area: 9.6 cm², diameter: 3.5 cm
    // collector diameter: 8.1 cm, area: 51.7 cm²

    input intake_camshaft;
    input exhaust_camshaft;
    input v_intake_camshaft;
    input v_exhaust_camshaft;
    input flip_display: false;
    
    alias output __out: head;
    generic_cylinder_head head(
        chamber_volume: 51.533 * units.cc,
        intake_runner_volume: 214.5 * units.cc,
        intake_runner_cross_section_area: 13.8 * units.cm2,
        exhaust_runner_volume: 71.5 * units.cc,
        exhaust_runner_cross_section_area: 9.6 * units.cm2,

        intake_port_flow: intake_flow,
        exhaust_port_flow: exhaust_flow,
        valvetrain: vtec_valvetrain(
            intake_camshaft: intake_camshaft,
            exhaust_camshaft: exhaust_camshaft,
            vtec_intake_camshaft: v_intake_camshaft,
            vtec_exhaust_camshaft: v_exhaust_camshaft,
            min_rpm: 3000 * units.rpm
        ),
        flip_display: flip_display
    )
}

public node eng {
    alias output __out: engine;

    engine engine(
        name: "VAG 6.0 W12",
        starter_torque: 150 * units.Nm,
        starter_speed: 650 * units.rpm,
        redline: redline * units.rpm,
        fuel: s90(),
        throttle_gamma: 1.5,
        jitter: 0.5,
        noise: 0.1,
        hf_gain: 0.001,
        simulation_frequency: 8000
    )

    wires wires()
    
    connecting_rod_parameters cr_params(
        mass: con_rod_mass * units.g,
        moment_of_inertia: rod_moment_of_inertia(
            mass: con_rod_mass * units.g,
            length: con_rod * units.mm
        ),
        center_of_mass: 0.0,
        length: con_rod * units.mm
    )

    label crank_moment(disk_moment_of_inertia(mass: crank_mass * units.kg, radius: (stroke / 2.0) * units.mm))
    label flywheel_moment(disk_moment_of_inertia(mass: flywheel_mass * units.kg, radius: (stroke * 3.5) * units.mm))
    label other_moment(disk_moment_of_inertia(mass: 1 * units.kg, radius: 1.0 * units.cm))

    crankshaft c0(
        throw: (stroke / 2.0) * units.mm,
        flywheel_mass: flywheel_mass * units.kg,
        mass: crank_mass * units.kg,
        friction_torque: 9.0 * units.Nm,
        moment_of_inertia: crank_moment + flywheel_moment + other_moment,
        position_x: 0.0,
        position_y: 0.0,
        tdc: 0.0
    )

    // even firing crank
    rod_journal rj0_0(angle: rot * 0 + rot90 + vee / 2 + vree / 2)
    rod_journal rj0_1(angle: rot * 0 + rot90 + vee / 2 + vree / 2 + phase_shift)
    rod_journal rj1_0(angle: rot * 8 + rot90 + vee / 2 - vree / 2)
    rod_journal rj1_1(angle: rot * 8 + rot90 + vee / 2 - vree / 2 + phase_shift)
    rod_journal rj2_0(angle: rot * 4 + rot90 + vee / 2 + vree / 2)
    rod_journal rj2_1(angle: rot * 4 + rot90 + vee / 2 + vree / 2 + phase_shift)
    rod_journal rj3_0(angle: rot * 10 + rot90 + vee / 2 - vree / 2)
    rod_journal rj3_1(angle: rot * 10 + rot90 + vee / 2 - vree / 2 + phase_shift)
    rod_journal rj4_0(angle: rot * 2 + rot90 + vee / 2 + vree / 2)
    rod_journal rj4_1(angle: rot * 2 + rot90 + vee / 2 + vree / 2 + phase_shift)
    rod_journal rj5_0(angle: rot * 6 + rot90 + vee / 2 - vree / 2)
    rod_journal rj5_1(angle: rot * 6 + rot90 + vee / 2 - vree / 2 + phase_shift)
    
    c0
        .add_rod_journal(rj0_0)
        .add_rod_journal(rj0_1)
        .add_rod_journal(rj1_0)
        .add_rod_journal(rj1_1)
        .add_rod_journal(rj2_0)
        .add_rod_journal(rj2_1)
        .add_rod_journal(rj3_0)
        .add_rod_journal(rj3_1)
        .add_rod_journal(rj4_0)
        .add_rod_journal(rj4_1)
        .add_rod_journal(rj5_0)
        .add_rod_journal(rj5_1)

    piston_parameters piston_params(
        mass: 450 * units.g,
        blowby: k_28inH2O(0.026),
        compression_height: compression_height,
        wrist_pin_position: 0.0,
        displacement: 0.0
    )

    cylinder_bank_parameters bank_params(
        bore: bore * units.mm,
        deck_height: (con_rod + stroke / 2.0) * units.mm + compression_height
    )

    label collector_cross_section_area(25.8)

    label exhaust_pipe_length_0(353.0) // cm
    label exhaust_volume_0(collector_cross_section_area * exhaust_pipe_length_0 / 100.0) // Litres
    
    label exhaust_pipe_length_1(350.0) // cm
    label exhaust_volume_1(collector_cross_section_area * exhaust_pipe_length_1 / 100.0) // Litres
    
    label spacing_factor(0.75)
    label flange_density(1.0 * exhaust_delay_coeff)

    intake intake(
        plenum_volume: 5.9 * units.L,
        plenum_cross_section_area: 83.0 * units.cm2,
        runner_length: 15.5 * units.cm,
        intake_flow_rate: k_carb(682.3),
        idle_flow_rate: k_carb(0.03),
        idle_throttle_plate_position: 0.9972,
        runner_flow_rate: k_carb(119.4),
        velocity_decay: 0.05
    )
    
    exhaust_system_parameters es_params0(
        outlet_flow_rate: k_carb(582.8),
        collector_cross_section_area: 25.8 * units.cm2,
        length: exhaust_pipe_length_0 * units.cm,
        primary_tube_length: 24.6 * units.cm,
        primary_flow_rate: k_carb(199.1),
        velocity_decay: 1.0,
        volume: (exhaust_volume_0 + 3.0) * units.L
    )
    
    exhaust_system_parameters es_params1(
        outlet_flow_rate: k_carb(582.8),
        collector_cross_section_area: 25.8 * units.cm2,
        length: exhaust_pipe_length_1 * units.cm,
        primary_tube_length: 24.6 * units.cm,
        primary_flow_rate: k_carb(199.1),
        velocity_decay: 1.0,
        volume: (exhaust_volume_1 + 3.0) * units.L
    )

    exhaust_system exhaust0(es_params0, impulse_response: impulse_response(filename: "../../impulse_responses/smooth_39.wav", volume: 0.0005))
    exhaust_system exhaust1(es_params1, impulse_response: impulse_response(filename: "../../impulse_responses/smooth_39.wav", volume: 0.0005))
 
    cylinder_bank b0(bank_params, angle: vee / 2 + vree / 2, position_x: -8.60443 * units.mm, position_y: -9.06718 * units.mm)
    cylinder_bank b1(bank_params, angle: vee / 2 - vree / 2, position_x: 10.98521 * units.mm, position_y: 5.96448 * units.mm)
    cylinder_bank b2(bank_params, angle: -vee / 2 + vree / 2, position_x: -10.98521 * units.mm, position_y: 5.96448 * units.mm)
    cylinder_bank b3(bank_params, angle: -vee / 2 - vree / 2, position_x: 8.60443 * units.mm, position_y: -9.06718 * units.mm)

    // cylinder_bank b0(bank_params, angle: vee / 2 + vree / 2)
    // cylinder_bank b1(bank_params, angle: vee / 2 - vree / 2)
    // cylinder_bank b2(bank_params, angle: -vee / 2 + vree / 2)
    // cylinder_bank b3(bank_params, angle: -vee / 2 - vree / 2)
    
    label pl0 ((5.5 * bore * spacing_factor / flange_density) * units.mm)
    label pl1 ((4.5 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl2 ((4.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl3 ((2.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl4 ((1.5 * bore * spacing_factor / flange_density) * units.mm)
    label pl5 ((1.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    
    label pl6 ((5.5 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl7 ((4.5 * bore * spacing_factor / flange_density) * units.mm)
    label pl8 ((4.0 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl9 ((2.0 * bore * spacing_factor / flange_density) * units.mm)
    label pl10 ((1.5 * bore * spacing_factor / flange_density + bore) * units.mm)
    label pl11 ((1.0 * bore * spacing_factor / flange_density) * units.mm)

    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl0,
            ignition_wire: wires.wire1
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl1,
            ignition_wire: wires.wire2
        )
    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl2,
            ignition_wire: wires.wire3
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl3,
            ignition_wire: wires.wire4
        )
    b0.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl4,
            ignition_wire: wires.wire5
        )
    b1.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5_0,
            intake: intake,
            exhaust_system: exhaust0,
            primary_length: pl5,
            ignition_wire: wires.wire6
        )
        
    b2.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj0_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl6,
            ignition_wire: wires.wire7
        )
    b3.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj1_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl7,
            ignition_wire: wires.wire8
        )
    b2.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj2_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl8,
            ignition_wire: wires.wire9
        )
    b3.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj3_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl9,
            ignition_wire: wires.wire10
        )
    b2.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj4_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl10,
            ignition_wire: wires.wire11
        )
    b3.add_cylinder(
            piston: piston(piston_params),
            connecting_rod: connecting_rod(cr_params),
            rod_journal: rj5_1,
            intake: intake,
            exhaust_system: exhaust1,
            primary_length: pl11,
            ignition_wire: wires.wire12
        )

    engine
        .add_cylinder_bank(b0)
        .add_cylinder_bank(b1)
        .add_cylinder_bank(b2)
        .add_cylinder_bank(b3)

    engine.add_crankshaft(c0)

    camshaft_builder_even camshaft()

    b0.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_0,
            exhaust_camshaft: camshaft.exhaust_cam_0,
            v_intake_camshaft: camshaft.v_intake_cam_0,
            v_exhaust_camshaft: camshaft.v_exhaust_cam_0,
            flip_display: true
        )
    )
    b1.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_1,
            exhaust_camshaft: camshaft.exhaust_cam_1,
            v_intake_camshaft: camshaft.v_intake_cam_1,
            v_exhaust_camshaft: camshaft.v_exhaust_cam_1,
            flip_display: true
        )
    )
    
    b2.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_2,
            exhaust_camshaft: camshaft.exhaust_cam_2,
            v_intake_camshaft: camshaft.v_intake_cam_2,
            v_exhaust_camshaft: camshaft.v_exhaust_cam_2
        )
    )
    b3.set_cylinder_head (
        head(
            intake_camshaft: camshaft.intake_cam_3,
            exhaust_camshaft: camshaft.exhaust_cam_3,
            v_intake_camshaft: camshaft.v_intake_cam_3,
            v_exhaust_camshaft: camshaft.v_exhaust_cam_3
        )
    )

    // ignition_timing.py v0.1 r=1.21
    function timing_curve(1000 * units.rpm)
    timing_curve
        .add_sample(0000 * units.rpm, 10.0 * units.deg)
        .add_sample(1000 * units.rpm, 12.1 * units.deg)
        .add_sample(2000 * units.rpm, 14.6 * units.deg)
        .add_sample(3000 * units.rpm, 17.7 * units.deg)
        .add_sample(4000 * units.rpm, 21.4 * units.deg)
        .add_sample(5000 * units.rpm, 25.9 * units.deg)
        .add_sample(6000 * units.rpm, 31.4 * units.deg)
        .add_sample(7000 * units.rpm, 38.0 * units.deg)

    engine.add_ignition_module(
        eng_distributor_even(
            wires: wires,
            timing_curve: timing_curve
        )
    )
}

// Phaeton W12 2004
public node veh {
    alias output __out: vehicle;
    vehicle vehicle(
        mass: 2359  * units.kg,
        drag_coefficient: 0.32,
        cross_sectional_area: (1903 * units.mm) * (1450 * units.mm),
        diff_ratio: 2.91,
        tire_radius: (724.0 / 2) * units.mm,
        rolling_resistance: 25
        )
}

// Phaeton W12 2004
private node trn {
    alias output __out:
        transmission(
            max_clutch_torque: 850 * units.Nm,
            max_clutch_flex: 20 * units.deg,
            limit_clutch_flex: true,
            clutch_stiffness: 15 * units.Nm / units.deg,
            clutch_damping: 0.5,
            simulate_flex: true
        )
        .add_gear(3.57)
        .add_gear(2.20)
        .add_gear(1.51)
        .add_gear(1.00)
        .add_gear(0.8);
}

run(
    engine: eng(),
    transmission: trn(),
    vehicle: veh()
)
